!$Id: init_2d_ncdf.F90,v 1.2 2003-04-07 12:48:11 kbk Exp $
#include "cppdefs.h"
!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: init_2d_ncdf -
!
! !INTERFACE:
   subroutine init_2d_ncdf(fn,title,starttime)
!
! !DESCRIPTION:
!
! !USES:
   use ncdf_2d
   use domain, only: ioff,joff,imin,imax,jmin,jmax
   use domain, only: grid_type
   use meteo, only: metforcing,calc_met
   IMPLICIT NONE
!
! !INPUT PARAMETERS:
   character(len=*), intent(in)	:: fn,title,starttime
!
! !INPUT/OUTPUT PARAMETERS:
!
! !OUTPUT PARAMETERS:
!
! !REVISION HISTORY:
!
!  $Log: init_2d_ncdf.F90,v $
!  Revision 1.2  2003-04-07 12:48:11  kbk
!  uses metfocing variable
!
!  Revision 1.1.1.1  2002/05/02 14:01:48  gotm
!  recovering after CVS crash
!
!  Revision 1.5  2001/10/26 12:18:06  bbh
!  No actual storing of data in init_2d_ncdf.F90 -> save_2d_ncdf.F90
!
!  Revision 1.4  2001/09/27 08:35:10  bbh
!  Saving meteo again - in .2d.nc file
!
!  Revision 1.3  2001/09/24 14:13:25  bbh
!  xc and yc have changing shape depending on grid_type
!
!  Revision 1.2  2001/09/19 11:20:32  bbh
!  Explicit de-allocates memory when -DFORTRAN90
!
!  Revision 1.1  2001/09/13 14:50:02  bbh
!  Cleaner and smaller NetCDF implementation + better axis support
!
! !LOCAL VARIABLES:
   integer	:: i,j,err
   character(32):: xname,yname,xunits,yunits,gridname
   integer	:: scalar(1),axisdim(1),f2_dims(2),f3_dims(3)
   REALTYPE	:: fv,mv,vr(2)
   character(len=80)	:: history,ts
!EOP
!-------------------------------------------------------------------------
!BOC
   include "netcdf.inc"

   xlen = imax-imin+1
   ylen = jmax-jmin+1

   select case (grid_type)
      case (1)
         xname = 'xax'
         yname = 'yax'
         xunits = 'meters'
         yunits = 'meters'
      case (2)
         xname = 'lon'
         yname = 'lat'
         xunits = 'degrees_east'
         yunits = 'degrees_north'
      case (3)
         xname = 'xdim'
         yname = 'ydim'
         xunits = 'curvi-x'
         yunits = 'curvi-y'
      case default
   end select

   err = nf_create(fn, NF_CLOBBER, ncid)
   if (err .NE. NF_NOERR) go to 10

!  dimensions
   err = nf_def_dim(ncid,xname,xlen,x_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,yname,ylen,y_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,'time',NF_UNLIMITED,time_dim)
   if (err .NE. NF_NOERR) go to 10

   f3_dims(3)	= time_dim
   f3_dims(2)	= y_dim
   f3_dims(1)	= x_dim

   history = 'Generated by getm, ver. '//RELEASE
   ts = 'seconds since '//starttime

!  info on offset, grid type and vertical coordinates
   scalar(1) = 0
   err = nf_def_var(ncid,'grid_type',NF_INT,0,scalar,grid_type_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,grid_type_id,units=gridname)

   err = nf_def_var(ncid,'ioff',NF_INT,0,scalar,ioff_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,ioff_id,long_name='index offset (i)')

   err = nf_def_var(ncid,'joff',NF_INT,0,scalar,joff_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,joff_id,long_name='index offset (j)')

!  time
   axisdim(1) = time_dim
   err = nf_def_var(ncid,'time',NF_REAL,1,axisdim,time_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,time_id,units=trim(ts),long_name='time')

!  coordinate variables
#ifndef CURVILINEAR
   axisdim(1) = x_dim
   err = nf_def_var(ncid,xname,NF_REAL,1,axisdim,xc_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,xc_id,units=xunits)
   axisdim(1) = y_dim
   err = nf_def_var(ncid,yname,NF_REAL,1,axisdim,yc_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,yc_id,units=yunits)
#else
   if(grid_type .eq. 3) then
!  do something about curvi-linear coordinates - define xu,xv,....
   end if
#endif

!  now to the variables
!  bathymetry
   err = nf_def_var(ncid,'bathymetry',NF_REAL,2,f3_dims,bathymetry_id)
   if (err .NE. NF_NOERR) go to 10
   fv = -10.
   mv = -10.
   vr(1) = -5.
   vr(2) = 4000.
   call set_attributes(ncid,bathymetry_id,			&
   			long_name='bathymetry',units='meters', 	&
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  elevation
   err = nf_def_var(ncid,'elev',NF_REAL,3,f3_dims,elev_id)
   if (err .NE. NF_NOERR) go to 10
   fv = -10.
   mv = -10.
   vr(1) = -15.
   vr(2) =  15.
   call set_attributes(ncid,elev_id,long_name='elevation',units='meters', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

   fv = _ZERO_
   mv = _ZERO_
   vr(1) = -3.
   vr(2) =  3.
!  zonal velocity
   err = nf_def_var(ncid,'u',NF_REAL,3,f3_dims,u_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,u_id,long_name='zonal velocity',units='m/s', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  meridional velocity
   err = nf_def_var(ncid,'v',NF_REAL,3,f3_dims,v_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,v_id,long_name='meridional velocity',units='m/s', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  meteorology
   if (metforcing .and. save_meteo) then
      if (calc_met) then
         fv = _ZERO_; mv = _ZERO_; vr(1) = -50.; vr(2) =  50.
         err = nf_def_var(ncid,'u10',NF_REAL,3,f3_dims,u10_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,u10_id,long_name='U10',units='m/s', &
   			      FillValue=fv,missing_value=mv,valid_range=vr)
         err = nf_def_var(ncid,'v10',NF_REAL,3,f3_dims,v10_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,v10_id,long_name='U10',units='m/s', &
   			      FillValue=fv,missing_value=mv,valid_range=vr)

         fv = _ZERO_; mv = _ZERO_; vr(1) = 90.e3; vr(2) = 110.e3
         err = nf_def_var(ncid,'airp',NF_REAL,3,f3_dims,airp_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,airp_id,			&
                             long_name='air pressure',units='Pascal', &
                             FillValue=fv,missing_value=mv,valid_range=vr)

         fv = _ZERO_; mv = _ZERO_; vr(1) = 0; vr(2) = 325.
         err = nf_def_var(ncid,'t2',NF_REAL,3,f3_dims,t2_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,t2_id,			&
	                     long_name='temperature (2m)',units='Kelvin', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)

         fv = _ZERO_; mv = _ZERO_; vr(1) = 0; vr(2) = 100.
         err = nf_def_var(ncid,'hum',NF_REAL,3,f3_dims,hum_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,hum_id,			&
	                     long_name='humidity',units='kg/kg', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)

         fv = _ZERO_; mv = _ZERO_; vr(1) = 0.; vr(2) = 1.
         err = nf_def_var(ncid,'cc',NF_REAL,3,f3_dims,cc_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,cc_id,			&
	                     long_name='cloud cover',units='fraction', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      fv = _ZERO_; mv = _ZERO_; vr(1) = -1; vr(2) = 1.
      err = nf_def_var(ncid,'tausx',NF_REAL,3,f3_dims,tausx_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,tausx_id,		&
	                  long_name='surface stress - x',units='N/m2', &
   	                  FillValue=fv,missing_value=mv,valid_range=vr)

      err = nf_def_var(ncid,'tausy',NF_REAL,3,f3_dims,tausy_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,tausy_id,		&
	                  long_name='surface stress - y',units='N/m2', &
   	                  FillValue=fv,missing_value=mv,valid_range=vr)

      fv = _ZERO_; mv = _ZERO_; vr(1) = 0; vr(2) = 1500.
      err = nf_def_var(ncid,'swr',NF_REAL,3,f3_dims,swr_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,swr_id,			&
	                  long_name='short wave radiation',units='W/m2', &
   	                  FillValue=fv,missing_value=mv,valid_range=vr)

      fv = _ZERO_; mv = _ZERO_; vr(1) = -1000; vr(2) = 1000.
      err = nf_def_var(ncid,'shf',NF_REAL,3,f3_dims,shf_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,shf_id,			&
	                  long_name='surface heat fluxes',units='W/m2', &
   	                  FillValue=fv,missing_value=mv,valid_range=vr)
   end if

   fv = _ZERO_; mv = _ZERO_; vr(1) = -3.; vr(2) =  3.
!  divergence
   err = nf_def_var(ncid,'div',NF_REAL,3,f3_dims,surfdiv_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,surfdiv_id,long_name='divergence',units='s-1', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  residual currents - u and v
   err = nf_def_var(ncid,'res_u',NF_REAL,2,f3_dims,res_u_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,res_u_id,long_name='res. u',units='m/s', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

   err = nf_def_var(ncid,'res_v',NF_REAL,2,f3_dims,res_v_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,res_v_id,long_name='res. v',units='m/s', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  globals
   err = nf_put_att_text(ncid,NF_GLOBAL,'title',LEN_TRIM(title),title)
   if (err .NE. NF_NOERR) go to 10

   err = nf_put_att_text(ncid,NF_GLOBAL,'history',LEN_TRIM(history),history)
   if (err .NE. NF_NOERR) go to 10

   ! leave define mode
   err = nf_enddef(ncid)
   if (err .NE. NF_NOERR) go to 10

   return

   10 FATAL 'init_2d_ncdf: ',nf_strerror(err)
   stop 'init_2d_ncdf'
   end subroutine init_2d_ncdf
!EOC

!-----------------------------------------------------------------------
! Copyright (C) 2001 - Hans Burchard and Karsten Bolding (BBH)         !
!-----------------------------------------------------------------------
