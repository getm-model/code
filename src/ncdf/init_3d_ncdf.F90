!$Id: init_3d_ncdf.F90,v 1.1 2002-05-02 14:01:46 gotm Exp $
#include "cppdefs.h"
!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: init_3d_ncdf -
!
! !INTERFACE:
   subroutine init_3d_ncdf(fn,title,starttime)
!
! !DESCRIPTION:
!
! !USES:
   use ncdf_3d
   use domain, only: ioff,joff,imin,imax,jmin,jmax,xc,yc
   use domain, only: iimin,iimax,jjmin,jjmax,kmax
   use domain, only: grid_type,vert_cord
   IMPLICIT NONE
!
! !INPUT PARAMETERS:
   character(len=*), intent(in)	:: fn,title,starttime
!
! !INPUT/OUTPUT PARAMETERS:
!
! !OUTPUT PARAMETERS:
!
! !REVISION HISTORY:
!
!  $Log: init_3d_ncdf.F90,v $
!  Revision 1.1  2002-05-02 14:01:46  gotm
!  Initial revision
!
!  Revision 1.7  2001/10/25 16:16:20  bbh
!  No actual storing of data in init_3d_ncdf.F90 -> save_3d_ncdf.F90
!
!  Revision 1.6  2001/10/23 14:19:20  bbh
!  Stores h if general vertical coordinates
!
!  Revision 1.5  2001/10/23 07:37:17  bbh
!  Saving spm - if calc_spm and save_spm are both true
!
!  Revision 1.4  2001/10/22 08:03:13  bbh
!  Misplaced #else
!
!  Revision 1.3  2001/09/24 14:13:25  bbh
!  xc and yc have changing shape depending on grid_type
!
!  Revision 1.2  2001/09/19 11:20:32  bbh
!  Explicit de-allocates memory when -DFORTRAN90
!
!  Revision 1.1  2001/09/13 14:50:02  bbh
!  Cleaner and smaller NetCDF implementation + better axis support
!
! !LOCAL VARIABLES:
   integer	:: i,j,k,err
   character(32):: xname,yname,zname,xunits,yunits,zunits
   character(32):: gridname,vertname
   integer	:: scalar(1),axisdim(1),f3_dims(3),f4_dims(4)
   REALTYPE	:: fv,mv,vr(2)
   REALTYPE	:: x
   character(len=80)	:: history,ts
!EOP
!-------------------------------------------------------------------------
!BOC
   include "netcdf.inc"

   xlen = iimax-iimin+1
   ylen = jjmax-jjmin+1
   zlen = kmax+1

   select case (grid_type)
      case (1)
         xname = 'xax'
         yname = 'yax'
         xunits = 'meters'
         yunits = 'meters'
	 gridname='cartesian'
      case (2)
         xname = 'lon'
         yname = 'lat'
         xunits = 'degrees_east'
         yunits = 'degrees_north'
	 gridname='spherical'
      case (3)
         xname = 'xdim'
         yname = 'ydim'
         xunits = 'curvi-x'
         yunits = 'curvi-y'
	 gridname='curvi-linear'
      case default
   end select

   select case (vert_cord)
      case (1)
         zname = 'zax'
         zunits = 'sigma_level'
	 vertname='sigma coordinates'
      case (2)
         zname = 'zax'
         zunits = 'meters'
	 vertname='z-levels'
      case (3)
         zname = 'zax'
         zunits = 'level'
	 vertname='general vertical coordinates'
      case default
   end select

   err = nf_create(fn, NF_CLOBBER, ncid)
   if (err .NE. NF_NOERR) go to 10

!  dimensions
   err = nf_def_dim(ncid,xname,xlen,x_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,yname,ylen,y_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,zname,zlen,z_dim)
   if (err .NE. NF_NOERR) go to 10

   err = nf_def_dim(ncid,'time',NF_UNLIMITED,time_dim)
   if (err .NE. NF_NOERR) go to 10

   f3_dims(3)	= time_dim
   f3_dims(2)	= y_dim
   f3_dims(1)	= x_dim

   f4_dims(4)	= time_dim
   f4_dims(3)	= z_dim
   f4_dims(2)	= y_dim
   f4_dims(1)	= x_dim

   history = 'Generated by getm, ver. '//RELEASE
   ts = 'seconds since '//starttime

!  info on offset, grid type and vertical coordinates
   scalar(1) = 0
   err = nf_def_var(ncid,'grid_type',NF_INT,0,scalar,grid_type_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,grid_type_id,units=gridname)

   err = nf_def_var(ncid,'vert_cord',NF_INT,0,scalar,vert_cord_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,vert_cord_id,units=vertname)

   err = nf_def_var(ncid,'ioff',NF_INT,0,scalar,ioff_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,ioff_id,long_name='index offset (i)')

   err = nf_def_var(ncid,'joff',NF_INT,0,scalar,joff_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,joff_id,long_name='index offset (j)')

!  time
   axisdim(1) = time_dim
   err = nf_def_var(ncid,'time',NF_REAL,1,axisdim,time_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,time_id,units=trim(ts),long_name='time')

!  coordinate variables
#ifndef CURVILINEAR
   axisdim(1) = x_dim
   err = nf_def_var(ncid,xname,NF_REAL,1,axisdim,xc_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,xc_id,units=xunits)
   axisdim(1) = y_dim
   err = nf_def_var(ncid,yname,NF_REAL,1,axisdim,yc_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,yc_id,units=yunits)
#else
   if(grid_type .eq. 3) then
!  do something about curvi-linear coordinates - define xu,xv,....
   end if
#endif
   axisdim(1) = z_dim
   err = nf_def_var(ncid,zname,NF_REAL,1,axisdim,z_id)
   if (err .NE. NF_NOERR) go to 10
   call set_attributes(ncid,z_id,units=zunits)

!  bathymetry
   err = nf_def_var(ncid,'bathymetry',NF_REAL,2,f3_dims,bathymetry_id)
   if (err .NE. NF_NOERR) go to 10
   fv = -10.
   mv = -10.
   vr(1) = -5.
   vr(2) = 4000.
   call set_attributes(ncid,bathymetry_id,			&
   			long_name='bathymetry',units='meters', 	&
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  now to the variables
!  elevation
   err = nf_def_var(ncid,'elev',NF_REAL,3,f3_dims,elev_id)
   if (err .NE. NF_NOERR) go to 10
   fv = -10.
   mv = -10.
   vr(1) = -15.
   vr(2) =  15.
   call set_attributes(ncid,elev_id,long_name='elevation',units='meters', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  depth integrated zonal velocity
   err = nf_def_var(ncid,'u',NF_REAL,3,f3_dims,u_id)
   if (err .NE. NF_NOERR) go to 10
   fv = 0.
   mv = 0.
   vr(1) = -3.
   vr(2) =  3.
   call set_attributes(ncid,u_id,long_name='int. zonal vel.',units='m/s', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

!  depth integrated zonal velocity
   err = nf_def_var(ncid,'v',NF_REAL,3,f3_dims,v_id)
   if (err .NE. NF_NOERR) go to 10
   fv = 0.
   mv = 0.
   vr(1) = -3.
   vr(2) =  3.
   call set_attributes(ncid,v_id,long_name='int. meridional vel.',units='m/s', &
   			FillValue=fv,missing_value=mv,valid_range=vr)

   select case (vert_cord)
      case (1)
      case (2)
         STDERR 'store z-levels'
	 stop 'init_3d_ncdf'
      case (3)
         fv = 0.
         mv = 0.
         err = nf_def_var(ncid,'h',NF_REAL,4,f4_dims,h_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,h_id,long_name='layer thickness',	&
	                     units='meters',FillValue=fv,missing_value=mv)
      case default
   end select

   if (save_vel) then
      fv = _ZERO_
      mv = _ZERO_
      vr(1) = -3.
      vr(2) =  3.

!     zonal velocity
      err = nf_def_var(ncid,'uu',NF_REAL,4,f4_dims,uu_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,uu_id,long_name='zonal vel.',units='m/s', &
   			   FillValue=fv,missing_value=mv,valid_range=vr)

!     meridional velocity
      err = nf_def_var(ncid,'vv',NF_REAL,4,f4_dims,vv_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,vv_id,long_name='meridional vel.',units='m/s', &
   			   FillValue=fv,missing_value=mv,valid_range=vr)

!     vertical velocity
      err = nf_def_var(ncid,'w',NF_REAL,4,f4_dims,w_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,w_id,long_name='vertical vel.',units='m/s', &
   			   FillValue=fv,missing_value=mv,valid_range=vr)
   end if

   if (save_strho) then
      fv = _ZERO_
      mv = _ZERO_

      if (save_s) then
         vr(1) =  0.
         vr(2) = 40.
         err = nf_def_var(ncid,'salt',NF_REAL,4,f4_dims,salt_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,salt_id,long_name='salinity',units='PSU', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_t) then
         vr(1) =  0.
         vr(2) = 40.
         err = nf_def_var(ncid,'temp',NF_REAL,4,f4_dims,temp_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,temp_id,long_name='temperature',units='degC',&
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_rho) then
         vr(1) =  0.
         vr(2) = 30.
         err = nf_def_var(ncid,'sigma_t',NF_REAL,4,f4_dims,sigma_t_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,sigma_t_id,long_name='sigma_t',units='kg/m3',&
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if
   end if

   if (save_turb) then

      if (save_tke) then
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'tke',NF_REAL,4,f4_dims,tke_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,tke_id,long_name='TKE',units='m2/s2', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if

      if (save_num) then
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'num',NF_REAL,4,f4_dims,num_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,num_id,long_name='viscosity',units='m2/s', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if

!      if (save_nuh) then
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'nuh',NF_REAL,4,f4_dims,nuh_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,nuh_id,long_name='diffusivity',units='m2/s', &
   			     FillValue=fv,missing_value=mv,valid_range=vr)
!      end if

      if (save_eps) then
         vr(1) = 0.
         vr(2) = 0.2
         err = nf_def_var(ncid,'diss',NF_REAL,4,f4_dims,eps_id)
         if (err .NE. NF_NOERR) go to 10
         call set_attributes(ncid,eps_id,long_name='dissipation',units='m2/s3',&
   			     FillValue=fv,missing_value=mv,valid_range=vr)
      end if
   end if

   if (save_spm) then
      vr(1) =  0.
      vr(2) = 30.
      err = nf_def_var(ncid,'spm',NF_REAL,4,f4_dims,spm_id)
      if (err .NE. NF_NOERR) go to 10
      call set_attributes(ncid,spm_id,				&
                      long_name='suspended particulate matter',units='?????', &
                      FillValue=fv,missing_value=mv,valid_range=vr)
   end if

!  globals
   err = nf_put_att_text(ncid,NF_GLOBAL,'title',LEN_TRIM(title),title)
   if (err .NE. NF_NOERR) go to 10

   err = nf_put_att_text(ncid,NF_GLOBAL,'history',LEN_TRIM(history),history)
   if (err .NE. NF_NOERR) go to 10

   ! leave define mode
   err = nf_enddef(ncid)
   if (err .NE. NF_NOERR) go to 10

   return

   10 FATAL 'init_3d_ncdf: ',nf_strerror(err)
   stop 'init_3d_ncdf'
   end subroutine init_3d_ncdf
!EOC

!-----------------------------------------------------------------------
! Copyright (C) 2001 - Hans Burchard and Karsten Bolding (BBH)         !
!-----------------------------------------------------------------------
